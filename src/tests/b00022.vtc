varnishtest "Check the between_bytes_timeout behaves from backend definition"

feature SO_RCVTIMEO_WORKS

server s1 {
	rxreq
	send "HTTP/1.0 200 OK\r\nConnection: close\r\n\r\n"
	delay 1.5
	send "Baba\n"
} -start

varnish v1 -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host="${bad_ip}"; }

	sub vcl_init {
		if (!backend_dyn.create(name="b1", host="${s1_addr}",
		   		        port="${s1_port}",
					between_bytes_timeout = 1s)) {
			return(fail);
		}
	}

	sub vcl_recv {
		set req.backend_hint = backend_dyn.by_name("b1");
	}

	sub vcl_backend_response {
		set beresp.do_stream = false;
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 503
} -run

server s1 {
	rxreq
	send "HTTP/1.0 200 OK\r\nConnection: close\r\n\r\n"
	delay 0.5
	send "Baba\n"
	delay 0.5
	send "Baba\n"
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
} -run

varnish v1 -expect VBE.vcl1.b1.happy == 0
varnish v1 -expect MAIN.n_backend == 2
