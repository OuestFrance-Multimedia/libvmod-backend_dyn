varnishtest "The (struct backend).n_conn counter is never decremented"

server s1 {
	rxreq
	txresp

	rxreq
	txresp
} -start

varnish v1 -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host = "${bad_ip}"; }

	sub vcl_init {
		if (!backend_dyn.create(name="be", host="${s1_addr}",
					port="${s1_port}", max_connections=1)) {
			return(fail);
		}
	}

	sub vcl_recv {
		set req.backend_hint = backend_dyn.by_name("be");
	}
} -start

client c1 {
	txreq -url "/foo"
	rxresp
	expect resp.status == 200

	txreq -url "/bar"
	rxresp
	expect resp.status == 200
} -run

varnish v1 -expect backend_busy == 0
varnish v1 -expect MAIN.n_backend == 2
varnish v1 -expect VBE.vcl1.be.happy == 0
