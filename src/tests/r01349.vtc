varnishtest "Exact matching for varnishadm backend.set_health"

server s1 -repeat 2 {
	rxreq
	txresp -hdr "Backend: b1"
} -start

server s2 -repeat 2 {
       rxreq
       txresp -hdr "Backend: b"
} -start

varnish v1 -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host = "${bad_ip}"; }

	sub vcl_init {
		if (!backend_dyn.create(name="b1", host="${s1_addr}",
						   port="${s1_port}")) {
			return(fail);
		}
		if (!backend_dyn.create(name="b", host="${s2_addr}",
						  port="${s2_port}")) {
			return(fail);
		}
	}

	sub vcl_recv {
		return(pass);
	}

	sub vcl_backend_fetch {
		if (bereq.http.backend == "b1") {
			set bereq.backend = backend_dyn.by_name("b1");
		}
		else {
			set bereq.backend = backend_dyn.by_name("b");
		}
	}

} -start

varnish v1 -cliok "backend.list b"

client c1 {
	txreq -hdr "Backend: b1"
	rxresp
	expect resp.status == 200
	expect resp.http.backend == "b1"

	txreq -hdr "Backend: b"
	rxresp
	expect resp.status == 200
	expect resp.http.backend == "b"
} -run

varnish v1 -cliok "backend.set_health b sick"

client c1 {
	txreq -hdr "Backend: b1"
	rxresp
	expect resp.status == 200
	expect resp.http.backend == "b1"

	txreq -hdr "Backend: b"
	rxresp
	expect resp.status == 503
} -run

varnish v1 -clierr 106 "backend.set_health b(1.2.3.4:) healthy"
varnish v1 -expect MAIN.n_backend == 3
varnish v1 -expect VBE.vcl1.b1.happy == 0
varnish v1 -expect VBE.vcl1.b.happy == 0
