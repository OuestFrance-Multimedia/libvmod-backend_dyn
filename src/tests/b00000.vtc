varnishtest "Does anything get through at all ?"

server s1 {
       rxreq
       txresp -body "012345\n"
} -start

varnish v1 -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host="${bad_ip}"; }

	sub vcl_init {
		if (!backend_dyn.create(name="be", host="${s1_addr}",
		   				   port="${s1_port}")) {
			return(fail);
		}
	}

	sub vcl_backend_fetch {
		set bereq.backend = backend_dyn.by_name("be");
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
        expect resp.body == "012345\n"
}

client c1 -run

varnish v1 -cliok "backend.list -p"

varnish v1 -expect VBE.vcl1.be.bereq_hdrbytes > 0
varnish v1 -expect VBE.vcl1.be.bereq_bodybytes == 0
varnish v1 -expect VBE.vcl1.be.beresp_hdrbytes == 38
varnish v1 -expect VBE.vcl1.be.beresp_bodybytes == 7
varnish v1 -expect s_resp_bodybytes == 7
varnish v1 -expect s_resp_hdrbytes == 167
