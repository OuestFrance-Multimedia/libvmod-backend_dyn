varnishtest "Run a lot of transactions through"

server s0 {
	loop 10 {
		rxreq
		txresp -body "foo1"
	}
	rxreq
	txresp -hdr "Connection: close" -body "foo1"
	expect_close
} -dispatch

varnish v1 -arg "-Wpoll" -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host="${bad_ip}"; }

	sub vcl_init {
		if (!backend_dyn.create(name="s0", host="${s0_addr}",
		   		        	   port="${s0_port}")) {
			return(fail);
		}
	}

	sub vcl_recv {
		return (pass);
	}
	sub vcl_backend_fetch {
		set bereq.backend = backend_dyn.by_name("s0");
	}

} -start

client c1 {
	loop 20 {
		txreq -url /c1
		rxresp
		expect resp.status == 200
	}
} -start

client c2 {
	loop 20 {
		txreq -url /c2
		rxresp
		expect resp.status == 200
	}
} -start

client c3 {
	loop 20 {
		txreq -url /c3
		rxresp
		expect resp.status == 200
	}
} -start

client c1 -wait
client c2 -wait
client c3 -wait

varnish v1 -expect VBE.vcl1.s0.happy == 0
