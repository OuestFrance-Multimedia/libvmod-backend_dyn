varnishtest "Forcing health of backends"

server s1 -repeat 3 {
	rxreq
	txresp
} -start

varnish v1 -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host="${bad_ip}"; }

	probe p {
		.window = 8;
		.initial = 7;
		.threshold = 8;
		.interval = 10s;
	}

	sub vcl_init {
		if (!backend_dyn.create(name="s1", host="${s1_addr}",
		   		        	   port="${s1_port}",
						   probe=p)) {
			return(fail);
		}
	}

	sub vcl_recv {
		set req.backend_hint = backend_dyn.by_name("s1");
		return(pass);
	}

} -start

delay 1

varnish v1 -cliok "vcl.list"
varnish v1 -cliok "backend.list -p"
varnish v1 -cliok "backend.set_health s1 auto"
varnish v1 -cliok "backend.list -p"

client c1 {
	txreq
	rxresp
	expect resp.status == 200
} -run

varnish v1 -cliok "backend.list"
varnish v1 -cliok "backend.set_health s1 sick"
varnish v1 -cliok "backend.list"

client c1 {
	txreq
	rxresp
	expect resp.status == 503
} -run

varnish v1 -cliok "backend.list"
varnish v1 -cliok "backend.set_health s1 healthy"
varnish v1 -cliok "backend.list"

client c1 {
	txreq
	rxresp
	expect resp.status == 200
} -run

varnish v1 -clierr 106 "backend.set_health s1 foo"
varnish v1 -clierr 106 "backend.set_health s2 foo"
varnish v1 -clierr 106 "backend.set_health s2 auto"
varnish v1 -cliok "vcl.list"
varnish v1 -cliok "backend.list *"
varnish v1 -cliok "backend.list *.foo"
varnish v1 -cliok "backend.list vcl1.*"

varnish v1 -expect VBE.vcl1.s1.happy > 0
varnish v1 -expect MAIN.n_backend == 2
