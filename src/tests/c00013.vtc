varnishtest "Test parking second request on backend delay"

server s1 {
	rxreq
	expect req.url == "/foo"
	send "HTTP/1.0 200 OK\r\nConnection: close\r\n\r\n"
	delay .2
	sema r1 sync 2
	delay .2
	send "line1\n"
	delay .2
	sema r1 sync 2
	send "line2\n"
} -start

varnish v1 -vcl {
	import backend_dyn from "${vmod_topbuild}/src/.libs/libvmod_backend_dyn.so";

	backend dummy { .host="${bad_ip}"; }

	sub vcl_init {
		if (!backend_dyn.create(name="be", host="${s1_addr}",
		   		        	   port="${s1_port}")) {
			return(fail);
		}
	}

	sub vcl_backend_fetch {
		set bereq.backend = backend_dyn.by_name("be");
	}

	sub vcl_backend_response {
		set beresp.do_stream = false;
	}
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	txreq -url "/foo" -hdr "client: c1"
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 12
	expect resp.http.x-varnish == "1001"
} -start

sema r1 sync 2

client c2 {
	txreq -url "/foo" -hdr "client: c2"
	delay .2
	sema r1 sync 2
	rxresp
	expect resp.status == 200
	expect resp.bodylen == 12
	expect resp.http.x-varnish == "1004 1002"
} -run

client c1 -wait

varnish v1 -expect busy_sleep == 1
varnish v1 -expect busy_wakeup == 1
